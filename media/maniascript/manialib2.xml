<frame>
<script><!--
/**
 * ManiaLib for ManiaScript
 *
 * Min version: 2011-11-15_15_29
 * 
 * This framework is not finished, not documented, not very useful and quite 
 * possibly full of bugs. We'll also probably break everything at some point to
 * refactor the code. However it's LGPL, so you can use at your own risk :)
 * 
 * @see         http://code.google.com/p/manialib/
 * @copyright   Copyright (c) 2009-2011 NADEO (http://www.nadeo.com)
 * @license     http://www.gnu.org/licenses/lgpl.html LGPL License 3
 * @version     $Revision: 523 $:
 * @author      $Author: maximeraoust $:
 * @date        $Date: 2011-10-26 12:41:57 +0200 (mer., 26 oct. 2011) $:
 */
 
#Include "TextLib" as TextLib
#RequireContext CGameManialinkScriptHandler

////////////////////////////////////////////////////////////////////////////////
//
// Internal variables
// These are used by the framework ; you shouldn't interact with them directly!
//
////////////////////////////////////////////////////////////////////////////////

declare Boolean[Text] _manialib_visibility;
declare Text[][][Text][Text] _manialib_event_listeners;

////////////////////////////////////////////////////////////////////////////////
//
// Utils
//
////////////////////////////////////////////////////////////////////////////////

Real manialib_max(Real real1, Real real2)
{
	if(real1 > real2)
	{
		return real1;
	}
	return real2;
}

Real manialib_min(Real real1, Real real2)
{
	if(real1 < real2)
	{
		return real1;
	}
	return real2;
}

Text manialib_event2text(CGameManialinkScriptEvent::Type eventType)
{
	declare _bindings = [
		"mouseclick" => CGameManialinkScriptEvent::Type::MouseClick,
		"mouseover" => CGameManialinkScriptEvent::Type::MouseOver,
		"mouseout" => CGameManialinkScriptEvent::Type::MouseOut,
		"keypress" => CGameManialinkScriptEvent::Type::KeyPress
	];
	if(_bindings.exists(eventType))
	{
		return _bindings.keyof(eventType);
	}
	log("[WARNING] Unkown event type: "^eventType);
	return "";
}

Integer[] manialib_version2array(Text version)
{
	declare Integer[] versionArray;
	versionArray.add(TextLib::ToInteger(TextLib::SubString(version, 0, 4)));
	versionArray.add(TextLib::ToInteger(TextLib::SubString(version, 5, 2)));
	versionArray.add(TextLib::ToInteger(TextLib::SubString(version, 8, 2)));
	versionArray.add(TextLib::ToInteger(TextLib::SubString(version, 11, 2)));
	versionArray.add(TextLib::ToInteger(TextLib::SubString(version, 14, 2)));
	return versionArray;
}

Boolean manialib_compare_versions(Text version1, Text version2)
{
	declare v1 = manialib_version2array(version1);
	declare v2 = manialib_version2array(version2);
	for(i, 0, 4)
	{
		if(v1[i] > v2[i]) return True;
		if(v1[i] < v2[i]) return False;
	}
	return True;
}

////////////////////////////////////////////////////////////////////////////////
//
// Manipulation
//
////////////////////////////////////////////////////////////////////////////////

Void manialib_hide(Text controlId)
{
	declare CGameManialinkControl control = Page.MainFrame.GetFirstChild(controlId);
	if(control != Null)
	{
		control.Hide();
		_manialib_visibility[controlId] = False;
	}
	else
	{
		log("[WARNING] Trying to access an unkown element: " ^ controlId);
	}
}

Void manialib_show(Text controlId)
{
	declare CGameManialinkControl control = Page.MainFrame.GetFirstChild(controlId);
	if(control != Null)
	{
		control.Show();
		_manialib_visibility[controlId] = True;
	}
	else
	{
		log("[WARNING] Trying to access an unkown element: " ^ controlId);
	}
}

Boolean manialib_visible(Text controlId)
{
	if(_manialib_visibility.existskey(controlId))
	{
		return _manialib_visibility[controlId];
	}
	return True;
}

Void manialib_toggle(Text controlId)
{
	if(manialib_visible(controlId))
	{
		manialib_hide(controlId);
	}
	else
	{
		manialib_show(controlId);
	}
}

Void manialib_posx(Text controlId, Real posx)
{
	declare CGameManialinkControl control = Page.MainFrame.GetFirstChild(controlId);
	if(control != Null)
	{
		control.PosnX = posx;
	}
	else
	{
		log("[WARNING] Trying to access an unkown element: " ^ controlId);
	}
}

Void manialib_posy(Text controlId, Real posy)
{
	declare CGameManialinkControl control = Page.MainFrame.GetFirstChild(controlId);
	if(control != Null)
	{
		control.PosnY = posy;
	}
	else
	{
		log("[WARNING] Trying to access an unkown element: " ^ controlId);
	}
}

Void manialib_posz(Text controlId, Real posz)
{
	declare CGameManialinkControl control = Page.MainFrame.GetFirstChild(controlId);
	if(control != Null)
	{
		control.PosnZ = posz;
	}
	else
	{
		log("[WARNING] Trying to access an unkown element: " ^ controlId);
	}
}

// Weird function/hack for the tooltip feature
Void manialib_move_to_mouse(Text controlId, Real offsetX, Real offsetY, Real boundingBoxX, Real boundingBoxY)
{
	declare CGameManialinkControl control = Page.MainFrame.GetFirstChild(controlId);
	control.PosnX = manialib_min(MouseX + offsetX, 160.0 - boundingBoxX);
	control.PosnY = manialib_max(MouseY + offsetY, -90.0 + boundingBoxY);
}

Void manialib_settext(Text controlId, Text newtext)
{
	declare CGameManialinkLabel control <=> (Page.MainFrame.GetFirstChild(controlId) as CGameManialinkLabel);
	if(control != Null)
	{
		control.SetText(newtext);
	}
	else
	{
		log("[WARNING] Trying to access an unkown element: " ^ controlId);
	}
}

Void manialib_disable_links()
{
	// TODO Uncomment when pushed in update
	//Page.InhibitLinks = True;
}

Void manialib_enable_links()
{
	// TODO Uncomment when pushed in update
	//Page.InhibitLinks = False;
}

////////////////////////////////////////////////////////////////////////////////
//
// Callback handling (internal stuff)
//
////////////////////////////////////////////////////////////////////////////////

Void manialib_callback_execute(Text[] action)
{
	if(action.count == 0)
	{
		return;
	}
	switch(action[0])
	{
		case "manialink":
			OpenLink(action[1], ::LinkType::ManialinkBrowser);
			
		case "manialinkid":
			OpenLink(action[1], ::LinkType::ManialinkFromId);

		case "external":
			OpenLink(action[1], ::LinkType::ExternalBrowser);
			
		case "externalid":
			OpenLink(action[1], ::LinkType::ExternalFromId);

		case "goto":
			OpenLink(action[1], ::LinkType::Goto);
			
		case "goto":
			OpenLink(action[1], ::LinkType::GotoFromId);

		case "hide":
			manialib_hide(action[1]);
			
		case "show":
			manialib_show(action[1]);
			
		case "toggle":
			manialib_toggle(action[1]);
			
		case "posx":
			manialib_posx(action[1], TextLib::ToReal(action[2]));
			
		case "posy":
			manialib_posy(action[1], TextLib::ToReal(action[2]));
			
		case "posz":
			manialib_posz(action[1], TextLib::ToReal(action[2]));
			
		case "settext":
			manialib_settext(action[1], action[2]);

		case "manialib_move_to_mouse":
			// TODO Because of a bug in TextLib::ToReal(), we cannot use the params...
			manialib_move_to_mouse(action[1], 0.0, -2.0, 75.0, 11.0);
			
		case "manialib_disable_links":
			manialib_disable_links();
			
		case "manialib_enable_links":
			manialib_enable_links();

		// You can also just pass an empty array
		case "":
			True;
			
		case "none":
			True;
			
		default:
			log("[WARNING] Unkown action name: " ^ action[1]);
	}
}

////////////////////////////////////////////////////////////////////////////////
//
// Event listening
//
////////////////////////////////////////////////////////////////////////////////

Void manialib_event_add_listener(Text controlId, CGameManialinkScriptEvent::Type eventType, Text[] action)
{
	if(!_manialib_event_listeners.existskey(controlId))
	{
		_manialib_event_listeners[controlId] = Text[][][Text];
	}
	if(!_manialib_event_listeners[controlId].existskey(manialib_event2text(eventType)))
	{
		_manialib_event_listeners[controlId][manialib_event2text(eventType)] = Text[][];
	}
	_manialib_event_listeners[controlId][manialib_event2text(eventType)].add(action);
}

Void manialib_event_listener(CGameManialinkScriptEvent event)
{
	if(_manialib_event_listeners.existskey(event.ControlId))
	{
		declare eventTypeName = manialib_event2text(event.Type);
		if(_manialib_event_listeners[event.ControlId].existskey(eventTypeName))
		{
			foreach(action in _manialib_event_listeners[event.ControlId][eventTypeName])
			{
				manialib_callback_execute(action);
			}
		}
	}
}

Void manialib_main_loop()
{
	while(True)
	{
		foreach(Event in PendingEvents)
		{
			manialib_event_listener(Event);
		}
		yield;		 
	}
}

////////////////////////////////////////////////////////////////////////////////
//
// UI Primitives
//
////////////////////////////////////////////////////////////////////////////////

Void manialib_ui_dialog(Text controlId, Text message, Text[] action)
{
	manialib_hide("manialib-dialog");
	manialib_posx("manialib-dialog", 0.0);
	
	manialib_event_add_listener(controlId, CGameManialinkScriptEvent::Type::MouseClick, ["settext", "manialib-dialog-text", message]);
	manialib_event_add_listener(controlId, CGameManialinkScriptEvent::Type::MouseClick, ["show", "manialib-dialog"]);
	manialib_event_add_listener(controlId, CGameManialinkScriptEvent::Type::MouseClick, ["manialib_disable_links"]);
	
	manialib_event_add_listener("manialib-dialog-no", CGameManialinkScriptEvent::Type::MouseClick, ["hide", "manialib-dialog"]);
	manialib_event_add_listener("manialib-dialog-no", CGameManialinkScriptEvent::Type::MouseClick, ["manialib_enable_links"]);
	
	manialib_event_add_listener("manialib-dialog-yes", CGameManialinkScriptEvent::Type::MouseClick, ["hide", "manialib-dialog"]);
	manialib_event_add_listener("manialib-dialog-yes", CGameManialinkScriptEvent::Type::MouseClick, ["manialib_enable_links"]);
	manialib_event_add_listener("manialib-dialog-yes", CGameManialinkScriptEvent::Type::MouseClick, action);
}

Void manialib_ui_tooltip(Text controlId, Text message)
{
	manialib_hide("manialib-dialog");
	
	manialib_event_add_listener(controlId, CGameManialinkScriptEvent::Type::MouseOver, ["settext", "manialib-tooltip-text", message]);
	manialib_event_add_listener(controlId, CGameManialinkScriptEvent::Type::MouseOver, ["show", "manialib-tooltip"]);
	manialib_event_add_listener(controlId, CGameManialinkScriptEvent::Type::MouseOver, ["manialib_move_to_mouse", "manialib-tooltip", "0.0", "-2.0", "75.0", "11.0"]);
	manialib_event_add_listener(controlId, CGameManialinkScriptEvent::Type::MouseOut, ["hide", "manialib-tooltip"]);
}

-->
</script>
<frame posn="320 0 4" id="manialib-dialog">
	<quad sizen="320 200" halign="center" valign="center" style="Bgs1" substyle="BgDialogBlur" scriptevents="1"/>
	<frame posn="-60 35 0.1">
		<quad sizen="123 63" style="Bgs1InRace" substyle="Shadow"/>
		<quad posn="1.5 -1.5 0.1" sizen="120 60" bgcolor="fffe"/>
		<label id="manialib-dialog-text" posn="60 -25 0.8" sizen="120 7" halign="center" valign="center" style="TextTips" text="-" autonewline="1"/>
		<label id="manialib-dialog-yes" posn="55 -47 0.8" sizen="35 7" halign="right" style="CardButtonMedium" scriptevents="1" text="Yes"/>
		<label id="manialib-dialog-no" posn="65 -47 0.8" sizen="35 7" style="CardButtonMedium" scriptevents="1" text="No"/>
	</frame>
</frame>
<frame posn="300 0 4.9" id="manialib-tooltip">
	<quad sizen="75 11" id="manialib-tooltip-box" style="Bgs1InRace" substyle="BgTitle3_3"/>
	<label id="manialib-tooltip-text" posn="4 -5.5 0.1" sizen="67 7" valign="center2" style="TextTips"/>
</frame>
</frame>